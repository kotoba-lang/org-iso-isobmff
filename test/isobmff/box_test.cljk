(ns isobmff.box-test
  "isobmff.box smoke test: synthetic boxes verify tree structure and
   boundaries (ported from utsushi.container-test)."
  (:require [clojure.test :refer [deftest is]]
            [isobmff.box :as box]))

(defn- be32 [n]
  [(bit-and (bit-shift-right n 24) 0xff)
   (bit-and (bit-shift-right n 16) 0xff)
   (bit-and (bit-shift-right n 8) 0xff)
   (bit-and n 0xff)])

(defn- ascii [s] (mapv int s))

(defn- mk-box [type payload]
  (let [size (+ 8 (count payload))]
    (vec (concat (be32 size) (ascii type) payload))))

;; ftyp (8B header + 4B body) and moov>trak (child).
(def sample
  (vec (concat (mk-box "ftyp" (ascii "isom"))
               (mk-box "moov" (mk-box "trak" [])))))

(deftest box-tree
  (let [boxes (box/parse-boxes sample)
        types (map :type boxes)
        moov  (box/find-box boxes "moov")
        trak  (box/find-box boxes "trak")]
    (is (= types ["ftyp" "moov"]))
    (is (= 1 (count (:children moov))))
    (is (= "trak" (:type trak)))
    (is (nil? (box/find-box boxes "mdat")))))
