(ns isobmff.meta-test
  "AVIF still-image metadata via a synthetic minimal box tree
   (ftyp + meta>iprp>ipco>ispe). Ported from kasane.container-test's
   isobmff-avif case — this is the test that exercises the meta FullBox
   version+flags-skip fix (isobmff.box's unification of kasane.isobmff and
   utsushi.container)."
  (:require [clojure.test :refer [deftest is]]
            [isobmff.meta :as meta]))

(defn- u32 [n] [(bit-and (bit-shift-right n 24) 0xff) (bit-and (bit-shift-right n 16) 0xff)
                (bit-and (bit-shift-right n 8) 0xff) (bit-and n 0xff)])
(defn- ascii [s] (mapv int s))
(defn- box [type payload] (vec (concat (u32 (+ 8 (count payload))) (ascii type) payload)))

;; minimal AVIF: ftyp + meta>iprp>ipco>ispe
(defn- make-avif [w h]
  (let [ispe (box "ispe" (vec (concat (u32 0) (u32 w) (u32 h))))        ; version/flags + w + h
        ipco (box "ipco" ispe)
        iprp (box "iprp" ipco)
        meta (box "meta" (vec (concat (u32 0) iprp)))                   ; meta is a fullbox
        ftyp (box "ftyp" (vec (concat (ascii "avif") (u32 0) (ascii "avif"))))]
    (vec (concat ftyp meta))))

(deftest isobmff-avif
  (let [p (meta/parse (make-avif 320 240))]
    (is (= "avif" (:brand p)))
    (is (= 320 (:width p)))
    (is (= 240 (:height p)))
    (is (contains? (set (:boxes p)) "ispe"))
    (is (contains? (set (:boxes p)) "ipco"))))
